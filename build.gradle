plugins {
	id 'fabric-loom' version '1.1-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven { url "https://maven.shedaniel.me/" }
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}
	maven {
		name = "Ladysnake Libs"
		url = "https://ladysnake.jfrog.io/artifactory/mods"
	}
	maven {
		name = "Jitpack"
		url = "https://jitpack.io"
	}
	maven {
		url "https://maven.saps.dev/minecraft"
		content {
			includeGroup "dev.latvian.mods"
			includeGroup "dev.ftb.mods"
		}
	}
	maven {
		url = "https://modmaven.dev/"
		name = "modmaven"
	}
	maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
	flatDir {
		dirs "./modsWithCompat"
		content {
			includeGroup "mods.compat"
		}
	}
	maven {
		url "https://cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}

	maven { url "https://maven.jamieswhiteshirt.com/libs-release" }
	maven {
		name = "CottonMC"
		url = "https://server.bbkr.space/artifactory/libs-release"
	}
	maven { url "https://maven.nucleoid.xyz/" }
	maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
	maven { url 'https://maven.wispforest.io' }
	mavenCentral()
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	//Config
	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	// Compat mods
	modImplementation "com.terraformersmc:modmenu:${project.mod_menu_version}"
	modImplementation("me.lucko:fabric-permissions-api:0.1-SNAPSHOT")
	modImplementation("mods.compat:The_Graveyard_1.9_(FABRIC)_for_1.18.2")
	modImplementation "io.github.ladysnake:requiem:${project.requiem_version}"

	// Don't destroy blocks compat
	modImplementation("com.github.Draylar:magna:${project.magna_version}")

	// Required for some mod compatibility to work
	modImplementation "io.github.cottonmc:LibGui:${project.lib_gui_version}" // Required to work with levelz
	modImplementation "io.github.ladysnake:automatone:${project.automatone_version}"  // Required to work with requiem

	// Land claim compat mods
	modImplementation("mods.compat:flan-${project.flan_version}-fabric")
	modImplementation("dev.ftb.mods:ftb-chunks-fabric:${project.ftb_chunks_version}")
	modImplementation("eu.pb4:common-protection-api:${project.protection_api_version}")

	// Inventory Compat mods
	modImplementation "dev.emi:trinkets:${project.trinkets_version}"
	modImplementation("mods.compat:levelz-${project.levelz_version}")
	modImplementation("mods.compat:inventorio-${project.inventorio_version}")
	modImplementation "mods.compat:travelers-backpack-${project.travelers_backpack_version}"
	modImplementation("com.github.apace100:apoli:${project.apoli_version}") { transitive = false } // Origins inventories
	modImplementation "mods.compat:numismatic-overhaul-${project.numismatic_version}"

	// Compile only mods
	modImplementation "com.github.apace100:calio:${project.calio_version}"
	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cca_version}"
	modImplementation "dev.architectury:architectury:${project.architectury_version}"
	modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"
	modImplementation "dev.ftb.mods:ftb-teams-fabric:${project.ftb_teams_version}"
	modImplementation "software.bernie.geckolib:geckolib-fabric-1.18:${project.geckolib_version_fabric}"
	modImplementation "io.wispforest:owo-lib:${project.owo_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}