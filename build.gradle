plugins {
	id 'fabric-loom' version '1.7-SNAPSHOT'
	id 'maven-publish'
	id "me.modmuss50.mod-publish-plugin" version "0.6.3"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

publishMods {
	file = remapJar.archiveFile

	changelog = file("CHANGELOG.md").text.split("\n---")[0]
	type = STABLE

	displayName = "You're in Grave Danger [Fabric] " + project.mod_version
	modLoaders.add("fabric")

	curseforge {
		projectId = "544912"

		accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")

		minecraftVersionRange {
			start = "1.20"
			end = project.minecraft_version
		}

		javaVersions.add(java.sourceCompatibility)

		clientRequired = true
		serverRequired = true

		projectSlug = project.archives_base_name;
		requires("fabric-api", "cloth-config")
	}

	github {
		displayName = project.mod_version

		accessToken = providers.environmentVariable("GITHUB_TOKEN")
		repository = "B1n-ry/Youre-in-grave-danger"
		commitish = "1.20-fabric"
		tagName = project.minecraft_version + "-" + project.mod_version + "-fabric"
	}

	modrinth {
		accessToken = providers.environmentVariable("MODRINTH_TOKEN")
		projectId = "HnD1GX6e"

		minecraftVersionRange {
			start = "1.20"
			end = project.minecraft_version
		}

		requires("fabric-api", "cloth-config")
	}
}

repositories {
	maven { url "https://maven.shedaniel.me/" }  // cloth_config
	maven { url "https://maven.terraformersmc.com/" }  // mod_menu, trinkets
	maven { url "https://maven.ladysnake.org/releases"	}  // Apoli dependency
	maven { url "https://server.bbkr.space/artifactory/libs-release" }  // LibGUI
	maven { url "https://oss.sonatype.org/content/repositories/snapshots" }  // Permission API
	maven { url "https://jitpack.io" }  // Apoli
	maven { url "https://maven.nucleoid.xyz/" }  // Common Protection API
	maven { url 'https://maven.wispforest.io/releases' }  // Accessories
	maven { url 'https://maven.su5ed.dev/releases' }  // Accessories
	maven {  // Inventorio, travelers backpack, numismatic overhaul, levelz (curseforge maven)
		url "https://cursemaven.com"
		content { includeGroup "curse.maven" }
	}
	maven {
		url = "https://api.modrinth.com/maven"
		content { includeGroup "maven.modrinth" }
	}
}

dependencies {
	// Important constants idk
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Required dependencies
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.config_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	// Bundled
	modImplementation include("io.github.cottonmc:LibGui:${project.lib_gui_version}")
	modImplementation include("me.lucko:fabric-permissions-api:${project.permissions_version}")

	// Compat mods
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
	modCompileOnly "eu.pb4:common-protection-api:${project.protection_api_version}"

	// Extra soulbound/droprule modification mods
	modCompileOnly "curse.maven:orpheus-${project.orpheus_version}"
	modCompileOnly "curse.maven:respawn-obelisks-${project.respawn_obelisks_version}"

	// Inventory Mods
	modCompileOnly "dev.emi:trinkets:${project.trinkets_version}"
	modCompileOnly "curse.maven:inventorio-${project.inventorio_version}"
	modCompileOnly "maven.modrinth:travelersbackpack:${project.travelers_backpack_version}"
	modCompileOnly "maven.modrinth:numismatic-overhaul:${project.numismatic_version}"
	modCompileOnly "com.github.apace100:apoli:${project.apoli_version}"
	modCompileOnly "maven.modrinth:levelz:${project.levelz_version}"
	modCompileOnly "maven.modrinth:beans-backpacks:${project.beans_backpacks_version}"
	modCompileOnly "io.wispforest:accessories-fabric:${project.accessories_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
